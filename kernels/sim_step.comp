#version 450
layout (local_size_x = 32) in;

#include "droplet.glsl"

layout(binding = 0) buffer Droplets {
    Droplet droplets[];
};

layout (binding=1, r32f) uniform readonly image2D heightmap;
layout (binding=2, r32f) uniform image2D erosionmap;

layout(binding = 3) uniform SimulationParams {
    // Inertia
    float inertia;
    // Minimum slope for capacity calculation
    float min_slope;
    // Capacity for droplets to carry material
    float capacity_const;
    // Sediment dropped beyond capacity
    float deposition;
    // Sediment picked up under capacity
    float erosion;
    // Force of gravity
    float gravity;
    // Evaporation rate
    float evaporation; // TODO: 1- evap for speed!
};

vec2 gradient(vec2 pos) {
    ivec2 int_pos = ivec2(pos);
    vec2 uv = fract(pos);
    float tl = imageLoad(heightmap, int_pos + ivec2(0, 0)).r;
    float tr = imageLoad(heightmap, int_pos + ivec2(1, 0)).r;
    float bl = imageLoad(heightmap, int_pos + ivec2(0, 1)).r;
    float br = imageLoad(heightmap, int_pos + ivec2(1, 1)).r;
    return vec2(
        mix(tr - tl, br - bl, uv.y),
        mix(bl - tl, br - tr, uv.x)
    );
}

// Simulation step
void main() {
    uint gid = gl_GlobalInvocationID.x;

    vec2 old_pos = droplets[gid].pos;
    vec2 grad = gradient(droplets[gid].pos);

    // TODO: Random direction if `dir` == vec2(0)!
    droplets[gid].dir = mix(grad, droplets[gid].dir, inertia);

    droplets[gid].pos += droplets[gid].dir;

    float height_old = imageLoad(heightmap, ivec2(old_pos)).x;
    float height_new = imageLoad(heightmap, ivec2(droplets[gid].pos)).x;
    float height_diff = height_new - height_old;

    float capacity = 
        max(-height_diff, min_slope)
        * droplets[gid].vel
        * droplets[gid].water
        * capacity_const;
    
    float sediment_diff = 0.;
    if (droplets[gid].sediment > capacity) {
        sediment_diff = (droplets[gid].sediment - capacity) * deposition;
    } else {
        sediment_diff = -min((capacity - droplets[gid].sediment) * erosion, -height_diff);
    }

    // TODO: You actually need to add or subtract within a radius! (Which is different due to settling/piling)
    imageAtomicExchange(erosionmap, ivec2(old_pos), sediment_diff);

    droplets[gid].vel = sqrt(pow(droplets[gid].vel, 2) + height_diff * gravity);

    droplets[gid].water *= 1. - evaporation;

    // TODO: BOUNDS CHECKING
}


